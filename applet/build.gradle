group 'smpc-rsa'
version '1.0-SNAPSHOT'

// Buildscript configuration for the javacard-gradle plugin.
// Do not modify this particular block. Dependencies for the project are lower.
buildscript {
    repositories {
        mavenCentral()
        maven {
            url "https://javacard.pro/maven"
        }
        maven {
            url "https://deadcode.me/mvn"
        }
        // mavenLocal()
    }
    dependencies {
        classpath 'com.klinec:gradle-javacard:1.7.2'
    }
}

apply plugin: 'com.klinec.gradle.javacard'
apply plugin: 'idea'
sourceCompatibility = 1.8

// Common settings, definitions
final def rootPath = rootDir.absolutePath
final def libs = rootPath + '/libs'
final def libsSdk = rootPath + '/libs-sdks'

// Repositories for your project
repositories {
    mavenCentral()
    // mavenLocal() // for local maven repository if needed

    // Repository with JCardSim, Globalplatform, etc, ...
    maven {
        url  "https://dl.bintray.com/ph4r05/jcard"
    }
    maven {
        url "https://javacard.pro/maven"
    }
    maven {
        url "https://deadcode.me/mvn"
    }

    // Local lib repository
    flatDir {
        dirs libs
    }
}

// Dependencies for your project
dependencies {
    jcardsim 'com.klinec:jcardsim:3.0.5.11'

    testImplementation 'org.testng:testng:6.1.1'
    testImplementation 'junit:junit:4.13'
    testImplementation 'org.slf4j:slf4j-api:1.7.30'
    testImplementation 'org.slf4j:slf4j-log4j12:1.7.30'
    testImplementation 'org.apache.logging.log4j:log4j-core:2.13.2'

    // Include plugin as it has bundled GP & other tools.
    // Alternative: include GP manually, but the included
    // version has to be compatible with the plugin.
    runtimeOnly 'com.klinec:gradle-javacard:1.7.2'
}

task dumpClassPath(dependsOn: ['idea']) {
    doLast {
        println "Gradle classpath:\n- " + configurations.compile.files*.name.join("\n- ")
        println "-------\n"
        println "IDEA classpath: \n- " + file(project.name + ".iml").readLines()
                .grep(~/.*"jar:.*/).collect { it.split("[\\/]")[-3].trim() }.join("\n- ")
        println "-------\n"
    }
}

test {
    useTestNG() {
         includeGroups 'clientSignBasic', 'clientSignSetKeys',
                       'clientSignSetMessage', 'clientSignSignature',
                       'clientSignStressTest',

                       'clientFullBasic', 'clientFullGenerate',
                       'clientFullGetKeys', 'clientFullSetMessage',
                       'clientFullSignature', 'clientFullStressTest',

                       'serverBasic', 'serverGenerate', 'serverSetClientKeys',
                       'serverGetModulus', 'serverSetClientSignature',
                       'serverSign', 'serverGetSignature', 'serverStressTest'

        useDefaultListeners = true
    }
}

// JavaCard SDKs and libraries
final def JC212 = libsSdk + '/jc212_kit'
final def JC221 = libsSdk + '/jc221_kit'
final def JC222 = libsSdk + '/jc222_kit'
final def JC303 = libsSdk + '/jc303_kit'
final def JC304 = libsSdk + '/jc304_kit'
final def JC305 = libsSdk + '/jc305u1_kit'
final def JC305u2 = libsSdk + '/jc305u2_kit'
final def JC305u3 = libsSdk + '/jc305u3_kit'
final def JC310b43 = libsSdk + '/jc310b43_kit'

// Which JavaCard SDK to use - select
// In order to compile JC222 and lower you have to have Java SDK <= 1.8
// For more info on JavaCard vs JDK version requirements inspect:
//   https://github.com/martinpaljak/ant-javacard/wiki/Version-compatibility
//
// JC310b43 supports building also for lower versions (cap.targetsdk).
// If another SDK is selected, please comment the cap.targetsdk setting.
final def JC_SELECTED = JC310b43

javacard {

    //noinspection GroovyAssignabilityCheck
    config {
        jckit JC_SELECTED

        // Using custom repo with jcardsim
        debugGpPro true
        addSurrogateJcardSimRepo false
        addImplicitJcardSim false
        addImplicitJcardSimJunit false

        //noinspection GroovyAssignabilityCheck
        cap {
            packageName 'smpc_rsa'
            version '0.1'
            aid '01:02:03:04:05:06:07:08:09'
            output 'client_sign.cap'
            // javaversion '1.7'
            targetsdk JC305

            //noinspection GroovyAssignabilityCheck
            applet {
                className 'smpc_rsa.RSAClientSign'
                aid '01:02:03:04:05:06:07:08:09:01:02'
            }

            //noinspection GroovyAssignabilityCheck
            // dependencies {
            //    remote 'com.klinec:globalplatform:2.1.1'
            // }
        }

        //noinspection GroovyAssignabilityCheck
        cap {
            packageName 'smpc_rsa'
            version '0.1'
            aid '01:02:03:04:05:06:07:08:09'
            output 'client_full.cap'
            // javaversion '1.7'
            targetsdk JC305

            //noinspection GroovyAssignabilityCheck
            applet {
                className 'smpc_rsa.RSAClient'
                aid '01:02:03:04:05:06:07:08:09:01:03'
            }
        }

        //noinspection GroovyAssignabilityCheck
        cap {
            packageName 'smpc_rsa'
            version '0.1'
            aid '01:02:03:04:05:06:07:08:09'
            output 'server.cap'
            // javaversion '1.7'
            targetsdk JC305

            //noinspection GroovyAssignabilityCheck
            applet {
                className 'smpc_rsa.RSAServer'
                aid '01:02:03:04:05:06:07:08:09:01:04'
            }
        }
    }
}

